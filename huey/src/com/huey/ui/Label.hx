/*
 * Swivel
 * Copyright (C) 2012-2017, Newgrounds.com, Inc.
 * https://github.com/Herschel/Swivel
 *
 * Swivel is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * Swivel is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Swivel.  If not, see <http://www.gnu.org/licenses/>.
 */

package com.huey.ui;
import com.huey.events.Dispatcher;
import flash.text.TextFormat;

class Label extends Component {
	public var text(default, set) : String;
	private function set_text(v : String) : String {
		if (v == null) v = "";
		text = _implText.text = v;
		return v;
	}
	
	@forward(_implText) public var wordWrap : Bool;
	
	public var color(get, set) : Int;
	private function get_color() return _textFormat.color;
	private function set_color(v) {
		_textFormat.color = v;
		updateTextFormat();
		return v;
	}
	
	public var font(get, set) : String;
	private function get_font() return _textFormat.font;
	private function set_font(v) {
		_textFormat.font = v;
		updateTextFormat();
		return v;
	}
	
	public var size(get, set) : Float;
	private function get_size() return _textFormat.size;
	private function set_size(v) {
		_textFormat.size = v;
		updateTextFormat();
		return v;
	}
	
	public var bold(get, set) : Bool;
	private function get_bold() return _textFormat.bold;
	private function set_bold(v) {
		_textFormat.bold = v;
		updateTextFormat();
		return v;
	}
	
	public var editable(get, set) : Bool;
	private function get_editable() return _implText.type == flash.text.TextFieldType.INPUT;
	private function set_editable(v) {
		_implText.selectable = v;
		_implText.type = v ? flash.text.TextFieldType.INPUT : flash.text.TextFieldType.DYNAMIC;
		return v;
	}
	
	public var autoSize(get, set) : Bool;
	private function get_autoSize() return _implText.autoSize != flash.text.TextFieldAutoSize.NONE;
	private function set_autoSize(v) {
		_implText.autoSize = if(v) flash.text.TextFieldAutoSize.LEFT else flash.text.TextFieldAutoSize.NONE;
		if(v == false) {
			_implText.width = 100;
			_implText.height = 20;
		}
		return v;
	}
	
	public var align(get, set) : TextAlign;
	private function get_align() {
		return switch(_textFormat.align) {
			case flash.text.TextFormatAlign.LEFT:		TextAlign.left;
			case flash.text.TextFormatAlign.CENTER:		TextAlign.center;
			case flash.text.TextFormatAlign.RIGHT:		TextAlign.right;
			case flash.text.TextFormatAlign.JUSTIFY:	TextAlign.justify;
			default:									TextAlign.left;
		}
	}
	private function set_align(v) {
		_textFormat.align = switch(v) {
			case TextAlign.left:	flash.text.TextFormatAlign.LEFT;
			case TextAlign.center:	flash.text.TextFormatAlign.CENTER;
			case TextAlign.right:	flash.text.TextFormatAlign.RIGHT;
			case TextAlign.justify:	flash.text.TextFormatAlign.JUSTIFY;
		}
		updateTextFormat();
		return v;
	}
	
	public var letterSpacing(get, set) : Float;
	private function get_letterSpacing() return _textFormat.letterSpacing;
	private function set_letterSpacing(v) {
		_textFormat.letterSpacing = v;
		updateTextFormat();
		return v;
	}
	
	@forward(_implText.restrict) public var allowedCharacters : String;
	
	public var onUserEdited(default, null) : Dispatcher<UIEvent>;

	public function new(?text = "") {
		onUserEdited = new Dispatcher();
		_implText = new flash.text.TextField();
		_implText.width = 100;
		_implText.height = 20;
		_implText.addEventListener(flash.events.Event.CHANGE, textChangeHandler);
		_implText.addEventListener(flash.events.FocusEvent.FOCUS_OUT, function(_) dispatchChange());
		
		autoSize = true;
		_implText.selectable = false;
		_textFormat = new TextFormat("Arial", 12, 0x000000, false, false, false);
		updateTextFormat();
		super(_implText);
		this.text = text;
	}

	private var _implText : flash.text.TextField;
	private var _textFormat : flash.text.TextFormat;
	private var _changeTimer : haxe.Timer;
	
	/**
	 * Names of the fonts compiled into assets/SwivelFonts.swf, read once.
	 *
	 * A TextField needs embedFonts = true to use an embedded font, and false
	 * to use one installed on the machine. Getting it wrong renders nothing at
	 * all -- no fallback -- so it is decided per font name rather than being
	 * hardcoded. That lets the markup name any installed font while the
	 * original embedded ones keep working.
	 */
	private static var _embeddedFonts : Map<String, Bool>;
	private static var _deviceFonts : Map<String, Bool>;

	private static function readFontLists() : Void {
		if(_embeddedFonts != null) return;

		_embeddedFonts = new Map();
		_deviceFonts = new Map();

		try {
			for(font in flash.text.Font.enumerateFonts(false)) _embeddedFonts.set(font.fontName, true);

			// enumerateFonts(true) adds the machine's installed fonts, so
			// anything here that was not in the embedded pass is a device font.
			for(font in flash.text.Font.enumerateFonts(true)) {
				if(!_embeddedFonts.exists(font.fontName)) _deviceFonts.set(font.fontName, true);
			}
		} catch(error : Dynamic) {}
	}

	/**
	 * Whether the TextField should use an embedded font for `fontName`.
	 *
	 * Defaults to true, matching the original hardcoded behaviour. Fonts that
	 * come in through -swf-lib are not always reported by enumerateFonts even
	 * though they render perfectly with embedFonts = true, so assuming
	 * "embedded" is the safe answer -- guessing "device" for a font the
	 * machine does not have renders nothing at all.
	 *
	 * Only a positively identified installed font turns embedding off, which
	 * is what lets the markup name any system font.
	 */
	private static function shouldEmbed(fontName : String) : Bool {
		if(fontName == null) return true;

		readFontLists();

		if(_embeddedFonts.exists(fontName)) return true;
		if(_deviceFonts.exists(fontName)) return false;
		return true;
	}

	private inline function updateTextFormat() {
		_implText.embedFonts = shouldEmbed(_textFormat.font);
		_implText.defaultTextFormat = _textFormat;
		_implText.setTextFormat(_textFormat);
	}
	
	private function textChangeHandler(_) {
		if(_changeTimer != null) {
			_changeTimer.stop();
		}
		
		_changeTimer = new haxe.Timer(750);
		_changeTimer.run = dispatchChange;
	}
	
	private function dispatchChange() {
		if(this.text != _implText.text) {
			this.text = _implText.text;
			onUserEdited.dispatch({source: this});
		}
		if(_changeTimer != null) {
			_changeTimer.stop();
			_changeTimer = null;
		}
	}
}